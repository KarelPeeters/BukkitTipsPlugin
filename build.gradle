buildscript {
    ext.kotlin_version = "1.2.0"
    ext.bukkit_version = "1.12.2"

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "java"
    id "ru.endlesscode.bukkitgradle" version "0.6.7"
}

apply plugin: "kotlin"

group "github.flaghacker.tipsplugin"
version "0.0.1"

bukkit {
    version = bukkit_version

    meta {
        name = "TipsPlugin"
        main = "TipsPlugin"
        description = "Manages tips"
        authors = ["Flaghacker"]
    }
}

sourceSets {
    main.java.srcDirs = ["src/main/kotlin/"]
    test.java.srcDirs = []
}

sourceCompatibility = 1.8

//TODO remove once ru.endlesscode.bukkitgradle is fixed
downloadServerCore.onlyIf { false }
copyServerCore.onlyIf { false }
prepareServer.doLast() {
    new File("$projectDir/server/$bukkit_version/eula.txt").text = "eula=true"
}

//fat jar
jar {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

repositories {
    mavenCentral()
    //maven { url = "http://maven.spinalcraft.com"  }
    flatDir {
        dirs "libs"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    //TODO use actual maven dependency, host currently broken
    //compile "com.jam.commandler:Commandler:1.0.0"
    compile name: "Commandler-1.0.0"
}